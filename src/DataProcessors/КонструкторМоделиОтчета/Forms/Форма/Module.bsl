
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.ТипГенератора = "ТабличныйДокумент";
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьТекстМоделиОтчета(Команда)
	КомандаПолучитьТекстМоделиОтчетаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьСтруктуруМакета(Команда)
	Текст = ПолучитьСтруктуруМакета();
	Текст.Показать("Структура макета");
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруМакета()
	Перем МодельСхемыКомпоновки;
	Перем МодельНастройкиКомпоновки;
	Перем МодельМакетаКомпоновки;
	Перем ДанныеРасшифровки;
	ДокументРезультат = Новый ТабличныйДокумент;
	УстановитьБезопасныйРежим(Истина);
	Выполнить(Объект.ТекстМодели);
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку(ОбщийКлиентСервер.ОбъектВJSON(МодельМакетаКомпоновки.СтруктураМакета()));
	Возврат Текст; 
КонецФункции

&НаСервере
Функция ОтобратьЗначимыеАргументы(Аргументы)
	Для й = -Аргументы.ВГраница() По 0 Цикл
		Если ЗначениеЗаполнено(Аргументы[-й]) Тогда
			Прервать;
		КонецЕсли;
		Аргументы.Удалить(-й);
	КонецЦикла;
	Возврат СтрСоединить(РаботаСМассивом.АТДМассив(Аргументы)
			.Отобразить("?(ЗначениеЗаполнено(Элемент), '''{' + Формат(Число(Индекс + 1), 'ЧГ=') + '}''', '')", Аргументы)
			.ВМассив()
		, ", ")
	;
КонецФункции
	
&НаСервере
Процедура КомандаПолучитьТекстМоделиОтчетаНаСервере()
	СхемаКомпоновкиДанных = ОбщегоНазначения.ЗначениеИзСтрокиXML(Объект.ТекстСКД);
	МодельТекста = Общий.МодельТекста()
		.Добавить("МодельСхемыКомпоновки = Общий.МодельСхемыКомпоновкиДанных();")
	;
	//  Текст запроса
	Для Каждого НаборДанных Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			МодельТекста
				.Добавить("ТекстЗапроса{1} = """, НаборДанных.Имя)
				.ГруппировкаНачать()
					.Присоединить(СтрЗаменить(СтрЗаменить(НаборДанных.Запрос, """", """"""), Символы.ПС, Символы.ПС + "|"))
					.Присоединить("""")
				.ГруппировкаЗавершить()
				.Добавить(";")
			;
		КонецЕсли;
	КонецЦикла;
	//  Наборы данных
	Для Каждого НаборДанных Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			МодельТекста
				.Добавить("МодельСхемыКомпоновки.НаборДанныхЗапрос(ТекстЗапроса{1}, ""{1}"")", НаборДанных.Имя)
			;
		КонецЕсли;
		Измерения = РаботаСМассивом.АТДМассив(НаборДанных.Поля)
			.Отобрать("Элемент.Роль.Измерение")
			.Отобразить("Новый Структура('Поле, Значение', Элемент.Поле, Элемент)")
			.ВМассив()
		;
		Ресурсы = РаботаСМассивом.АТДМассив(СхемаКомпоновкиДанных.ПоляИтога)
			.Отобразить("Новый Структура('Поле, Значение', Элемент.ПутьКДанным, Элемент)")
			.ВМассив()
		;
		Периоды = РаботаСМассивом.АТДМассив(НаборДанных.Поля)
			.Отобрать("Элемент.Роль.НомерПериода > 0")
			.Отобразить("Новый Структура('Поле, НомерПериода, Значение', Элемент.ПутьКДанным, Элемент.Роль.НомерПериода, Элемент)")
			.СортироватьПо("НомерПериода")
			.ВМассив()
		;
		Реквизиты = РаботаСМассивом.АТДМассив(НаборДанных.Поля)
			.Отобрать("Параметры.НайтиЭлемент(СтрШаблон('Элемент.Поле = ''%1''', Элемент.Поле)) = Неопределено", , РаботаСМассивом.АТДМассив(Измерения))
			.Отобрать("Параметры.НайтиЭлемент(СтрШаблон('Элемент.Поле = ''%1''', Элемент.Поле)) = Неопределено", , РаботаСМассивом.АТДМассив(Ресурсы))
			.Отобрать("Параметры.НайтиЭлемент(СтрШаблон('Элемент.Поле = ''%1''', Элемент.Поле)) = Неопределено", , РаботаСМассивом.АТДМассив(Периоды))
			.ВМассив()
		;
		Если ЗначениеЗаполнено(Измерения) Тогда
			МодельТекста
				.ГруппировкаНачать()
				.Добавить(".Измерения()")
				.ГруппировкаНачать()
			;
			Для Каждого Поле Из Измерения Цикл
				ПолеНабораДанных = НаборДанных.Поля.Найти(Поле.Поле);
				Если ПолеНабораДанных = Неопределено Тогда
					ВычисляемоеПоле = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(Поле.Поле);
					Если ВычисляемоеПоле = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					//  Вычисляемое поле
					МодельТекста.Добавить(".ВычисляемоеПоле(""{1}"", , ""{2}"")", Поле.Поле, СтрЗаменить(ВычисляемоеПоле.Выражение, """", """"""));
					Продолжить;
				КонецЕсли;
				Роль = ПолеНабораДанных.Роль;
				Если Роль.Счет Тогда
					Если Роль.ВыражениеВидаСчета = Поле.Поле + ".Вид" Тогда
						МодельТекста
							.Добавить(".Счет(""{1}"")", Поле.Поле)
						;
					Иначе
						МодельТекста
							.Добавить(".Счет(""{1}"", ""{2}"")", Поле.Поле, Роль.ВыражениеВидаСчета)
						;
					КонецЕсли;
					Продолжить;
				КонецЕсли;
				
				МодельТекста
					.Добавить(".Поле(""{1}"")", Поле.Поле)
				;
			КонецЦикла;
			МодельТекста
				.ГруппировкаЗавершить()
				.ГруппировкаЗавершить()
			;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизиты) ИЛИ ЗначениеЗаполнено(Периоды) Тогда
			МодельТекста
				.ГруппировкаНачать()
				.Добавить(".Реквизиты()")
				.ГруппировкаНачать()
			;
			Для Каждого Поле Из Периоды Цикл
				МодельТекста
					.Добавить(".Период(""{1}"")", Поле.Поле)
				;
			КонецЦикла;
			Для Каждого Поле Из Реквизиты Цикл
				МодельТекста
					.Добавить(".Поле(""{1}"")", Поле.Поле)
				;
			КонецЦикла;
			МодельТекста
				.ГруппировкаЗавершить()
				.ГруппировкаЗавершить()
			;
		КонецЕсли;
		
		РесурсыНабора = РаботаСМассивом.Отобрать(Ресурсы, "Контекст.Найти(Элемент.Поле) <> Неопределено", НаборДанных.Поля);
		Ресурсы = РаботаСМассивом.Отобрать(Ресурсы, "Контекст.Найти(Элемент) = Неопределено", РесурсыНабора);
		
		Если ЗначениеЗаполнено(РесурсыНабора) Тогда
			МодельТекста
				.ГруппировкаНачать()
					.Добавить(".Ресурсы()")
					.ГруппировкаНачать()
			;
			Для Каждого Поле Из РесурсыНабора Цикл
				ПолеНабораДанных = НаборДанных.Поля.Найти(Поле.Поле);
				Роль = ПолеНабораДанных.Роль;
				Аргументы = Новый Массив;
				Аргументы.Добавить(ПолеНабораДанных.Поле);// Имя
				Аргументы.Добавить(?(ПолеНабораДанных.Заголовок <> ПолеНабораДанных.Поле, ПолеНабораДанных.Заголовок, ""));//  Заголовок
				Аргументы.Добавить(?(ПолеНабораДанных.Поле <> Поле.Поле, Поле.Поле, ""));// Путь
				Если Роль.Остаток Тогда
					Аргументы.Добавить(Роль.ГруппаОстатка);
					Если ЗначениеЗаполнено(Роль.ПолеСчета) Тогда
						Аргументы.Добавить(Роль.ТипБухгалтерскогоОстатка);
						Аргументы.Добавить(Роль.ПолеСчета);
						ОписаниеАргументов = ОтобратьЗначимыеАргументы(Аргументы);
						Если Роль.ТипОстатка = ТипОстаткаКомпоновкиДанных.НачальныйОстаток Тогда
							МодельТекста.Добавить(СтрШаблон(".НачальныйОстаток(%1)", ОписаниеАргументов), Аргументы);
						ИначеЕсли Роль.ТипОстатка = ТипОстаткаКомпоновкиДанных.КонечныйОстаток Тогда
							МодельТекста.Добавить(СтрШаблон(".КонечныйОстаток(%1)", ОписаниеАргументов), Аргументы);
						Иначе
							ВызватьИсключение "Неизвестный тип остатка " + Роль.ТипОстатка;
						КонецЕсли;
					Иначе
						ОписаниеАргументов = ОтобратьЗначимыеАргументы(Аргументы);
						Если Роль.ТипОстатка = ТипОстаткаКомпоновкиДанных.НачальныйОстаток Тогда
							МодельТекста.Добавить(СтрШаблон(".НачальныйОстаток(%1)", ОписаниеАргументов), Аргументы);
						ИначеЕсли Роль.ТипОстатка = ТипОстаткаКомпоновкиДанных.КонечныйОстаток Тогда
							МодельТекста.Добавить(СтрШаблон(".КонечныйОстаток(%1)", ОписаниеАргументов), Аргументы);
						Иначе
							ВызватьИсключение "Неизвестный тип остатка " + Роль.ТипОстатка;					
						КонецЕсли;
					КонецЕсли;
				Иначе
					ОписаниеАргументов = ОтобратьЗначимыеАргументы(Аргументы);
					МодельТекста.Добавить(СтрШаблон(".Сумма(%1)", ОписаниеАргументов), Аргументы);
				КонецЕсли;
			КонецЦикла;	
			МодельТекста
					.ГруппировкаЗавершить()
				.ГруппировкаЗавершить()
			;
		КонецЕсли;
		МодельТекста
			.Добавить(";")
		;
	КонецЦикла;
	//  Вычисляемые ресурсы
	Если ЗначениеЗаполнено(Ресурсы) Тогда
		Для Каждого Поле Из Ресурсы Цикл
			ПолеНабораДанных = НаборДанных.Поля.Найти(Поле.Поле);
			Если ПолеНабораДанных = Неопределено Тогда
				ВычисляемоеПоле = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(Поле.Поле);
				Если ВычисляемоеПоле = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				//  Вычисляемое поле
				МодельТекста.Добавить(".ВычисляемоеПоле(""{1}"", , ""{2}"")", Поле.Поле, СтрЗаменить(ВычисляемоеПоле.Выражение, """", """"""));
				Продолжить;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//  Вычисляемые поля
	
	//  Соединения наборов
	Если СхемаКомпоновкиДанных.СвязиНаборовДанных.Количество() > 0 Тогда
		МодельТекста
			.Добавить("МодельСхемыКомпоновки")
			.ГруппировкаНачать()
		;
		Для Каждого Соединение Из СхемаКомпоновкиДанных.СвязиНаборовДанных Цикл
			МодельТекста
				.Добавить(".{1}(""{2}"", ""{3}"")", ?(Соединение.Обязательная, "ВнутреннееСоединение", "ЛевоеСоединение"), Соединение.НаборДанныхИсточник, Соединение.НаборДанныхПриемник)
				.ГруппировкаНачать()
			;
			Если ЗначениеЗаполнено(Соединение.ВыражениеИсточник) И ЗначениеЗаполнено(Соединение.ВыражениеПриемник) Тогда
				Если Соединение.ВыражениеИсточник = Соединение.ВыражениеПриемник Тогда
					МодельТекста
						.Добавить(".Связь(""{1}"")", Соединение.ВыражениеИсточник)
					;
				Иначе
					МодельТекста
						.Добавить(".Связь(""{1} = {2}"")", Соединение.ВыражениеИсточник, Соединение.ВыражениеПриемник)
					;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(Соединение.УсловиеСвязи) Тогда
				МодельТекста
					.Добавить(".УсловиеСвязи(""{1}"")", Соединение.УсловиеСвязи)
				;
			КонецЕсли;
			Если ЗначениеЗаполнено(Соединение.Параметр) Тогда
				МодельТекста
					.Добавить(".ПараметрСвязи(""{1}"")", Соединение.Параметр)
				;
			КонецЕсли;
			Если ЗначениеЗаполнено(Соединение.НачальноеВыражение) Тогда
				МодельТекста
					.Добавить(".НачальноеВыражение(""{1}"")", Соединение.НачальноеВыражение)
				;
			КонецЕсли;
			МодельТекста
				.ГруппировкаЗавершить()
			;
		КонецЦикла;
		МодельТекста
			.ГруппировкаЗавершить()
			.Добавить(";")
		;
	КонецЕсли;
	//  Параметры
	Если ЗначениеЗаполнено(СхемаКомпоновкиДанных.Параметры) Тогда
		МодельТекста
			.Добавить("МодельСхемыКомпоновки")
			.ГруппировкаНачать()
				.Добавить(".Параметры()")
				.ГруппировкаНачать()
		;
		Для Каждого Параметр Из СхемаКомпоновкиДанных.Параметры Цикл
			МодельТекста.Добавить(".Параметр(""{1}"")", Параметр.Имя);
			Если ЗначениеЗаполнено(Параметр.Выражение) Тогда
				МодельТекста.Присоединить(".Выражение(""{1}"")", СтрЗаменить(Параметр.Выражение, """", """"""));
			КонецЕсли;
		КонецЦикла;
		МодельТекста
				.ГруппировкаЗавершить()
			.ГруппировкаЗавершить()
			.Добавить(";")
		;
	КонецЕсли;
	//////////////////////////////////////////////////
	//  Настройки
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	МодельТекста
		.Добавить("МодельНастройкиКомпоновки = Общий.МодельНастройкиКомпоновкиДанных(МодельСхемыКомпоновки.СхемаКомпоновкиДанных)")
		.ГруппировкаНачать()	
	;
	ВывестиВыбранныеПоля(МодельТекста, Настройки.Выбор.Элементы);
	Если ЗначениеЗаполнено(Настройки.Отбор.Элементы) Тогда
		МодельТекста
			.Добавить(".Условия()")
			.ГруппировкаНачать()	
		;
		ВывестиУсловияОтбора(МодельТекста, Настройки.Отбор.Элементы);
		МодельТекста
			.ГруппировкаЗавершить()
		;
	КонецЕсли;
	//  Структура
	Если ЗначениеЗаполнено(Настройки.Структура) Тогда
		МодельТекста
			.Добавить(".Структура()")
			.ГруппировкаНачать()
				.Добавить(".ГруппировкаНачать()")
				.ГруппировкаНачать()	
		;
		ВывестиГруппировку(МодельТекста, Настройки.Структура);
		МодельТекста
				.ГруппировкаЗавершить()
				.Добавить(".ГруппировкаЗавершить()")
			.ГруппировкаЗавершить()
		;
		Для Каждого ЭлементПорядка Из Настройки.Порядок.Элементы Цикл
			Если ТипЗнч(ЭлементПорядка) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
				//Автопорядок
				Продолжить;
			КонецЕсли;
			Если ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
				МодельТекста.Добавить(".Порядок(""{1}"")", ЭлементПорядка.Поле);
			Иначе
				МодельТекста.Добавить(".Порядок(""{1}"", ""-"")", ЭлементПорядка.Поле);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	МодельТекста
		.ГруппировкаЗавершить()
	;
	////////////////////////////////////////////////
	//  Параметры вывода
	ПараметрыВывода = РаботаСМассивом.АТДМассив(Настройки.ПараметрыВывода.Элементы)
		.Отобрать("Элемент.Использование")
		.Отобразить("Новый Структура('Параметр, Значение', Элемент.Параметр, Элемент.Значение)")
		.ВМассив()
	;
	Если ЗначениеЗаполнено(ПараметрыВывода) Тогда
		МодельТекста
			.ГруппировкаНачать()
				.Добавить(".ПараметрыВывода()")
				.ГруппировкаНачать()				
		;
		Для Каждого ПараметрВывода Из ПараметрыВывода Цикл
			МодельТекста.Добавить(".Параметр(""{1}"", ""{2}"")", ПараметрВывода.Параметр, ПараметрВывода.Значение);
		КонецЦикла;
		МодельТекста
				.ГруппировкаЗавершить()
			.ГруппировкаЗавершить()
		;
	КонецЕсли;
	МодельТекста
		.Добавить(";")
	;
	////////////////////////////////////////////////
	//  Вывод в табличный документ
	Если НЕ ЗначениеЗаполнено(Объект.ТипГенератора) ИЛИ Объект.ТипГенератора = "ТабличныйДокумент" Тогда
		МодельТекста
			.Добавить("МодельМакетаКомпоновки = Общий.МодельМакетаКомпоновкиДанных()")
			.ГруппировкаНачать()
				.Добавить("//  Формирование макета и вывод отчета")
				.Добавить(".Схема(МодельСхемыКомпоновки.СхемаКомпоновкиДанных)")
				.Добавить(".Настройки(МодельНастройкиКомпоновки.Настройки)")
				.Добавить(".Скомпоновать(ДанныеРасшифровки)")
			.ГруппировкаЗавершить()
			.Добавить(";")
			.Добавить("МодельМакетаКомпоновки.Вывести(ДокументРезультат);")
		;
	Иначе
		МодельТекста
			.Добавить("МодельМакетаКомпоновки = Общий.МодельМакетаКомпоновкиДанных()")
			.ГруппировкаНачать()
				.Добавить("//  Формирование макета и вывод отчета")
				.Добавить(".Схема(МодельСхемыКомпоновки.СхемаКомпоновкиДанных)")
				.Добавить(".Настройки(МодельНастройкиКомпоновки.Настройки)")
				.Добавить(".Скомпоновать{1}(ДанныеРасшифровки)", ?(Объект.ТипГенератора = "ДеревоЗначений", "ДеревоЗначений", "ТаблицуЗначений"))
			.ГруппировкаЗавершить()
			.Добавить(";")
			.Добавить("Результат = МодельМакетаКомпоновки.Вывести();")
		;
	КонецЕсли;
	Объект.ТекстМодели = МодельТекста.ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьСхему1(Команда)
	ЗагрузитьСхему("Схема1");
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСхему(ИмяСхемы)
	_Обработка = РеквизитФормыВЗначение("Объект");
	Макет = _Обработка.ПолучитьМакет(ИмяСхемы);
	Объект.ТекстСКД = ОбщегоНазначения.ЗначениеВСтрокуXML(Макет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьСхемуДемоОборотноСальдоваяВедомость(Команда)
	ЗагрузитьСхему("ДемоОборотноСальдоваяВедомость");
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьСхемуДемоОтчетПоСчетамНаОплатуГлобальный(Команда)
	ЗагрузитьСхему("ДемоОтчетПоСчетамНаОплатуГлобальный");
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформировать(Команда)
	КомандаСформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаСформироватьНаСервере()
	Перем МодельСхемыКомпоновки;
	Перем МодельНастройкиКомпоновки;
	Перем МодельМакетаКомпоновки;
	Перем ДанныеРасшифровки;
	Перем Результат;
	ДокументРезультат = Новый ТабличныйДокумент;
	УстановитьБезопасныйРежим(Истина);
	Выполнить(Объект.ТекстМодели);
	Объект.ДокументРезультат = ДокументРезультат;
	Если ТипЗнч(Результат) = Тип("ТаблицаЗначений") Тогда
		Объект.Результат = ОбщийКлиентСервер.ОбъектВJSON(РаботаСКоллекцией.ТаблицаЗначенийВСтруктуру(Результат, Истина));
	ИначеЕсли ТипЗнч(Результат) = Тип("ДеревоЗначений") Тогда
		Объект.Результат = ОбщийКлиентСервер.ОбъектВJSON(РаботаСКоллекцией.ДеревоЗначенийВСтруктуру(Результат, Истина));
	Иначе
		Объект.Результат = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьСхемуКомпоновкиДанных(Команда)
	Текст = ПолучитьСхемуКомпоновкиДанных();
	Текст.Показать("Схема компоновки данных");
КонецПроцедуры

&НаСервере
Функция ПолучитьСхемуКомпоновкиДанных()
	Перем МодельСхемыКомпоновки;
	Перем МодельНастройкиКомпоновки;
	Перем МодельМакетаКомпоновки;
	Перем ДанныеРасшифровки;
	ДокументРезультат = Новый ТабличныйДокумент;
	УстановитьБезопасныйРежим(Истина);
	Выполнить(Объект.ТекстМодели);
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку(ОбщегоНазначения.ЗначениеВСтрокуXML(МодельСхемыКомпоновки.СхемаКомпоновкиДанных));
	Возврат Текст; 
КонецФункции

Процедура ВывестиГруппировку(МодельТекста, Группировка)
	Для Каждого ЭлементГруппировки Из Группировка Цикл
		Для Каждого ПолеГруппировки Из ЭлементГруппировки.ПоляГруппировки.Элементы Цикл
			Если ТипЗнч(ПолеГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
				МодельТекста.Добавить(".ПолеГруппировки(""*"")");
			Иначе
				МодельТекста.Добавить(".ПолеГруппировки(""{1}"")", ПолеГруппировки.Поле);
			КонецЕсли;
		КонецЦикла;
		ВывестиВыбранныеПоля(МодельТекста, ЭлементГруппировки.Выбор.Элементы);
		Если ЗначениеЗаполнено(ЭлементГруппировки.Структура) Тогда
			МодельТекста
				.Добавить(".ГруппировкаНачать()")
					.ГруппировкаНачать()	
			;
			ВывестиГруппировку(МодельТекста, ЭлементГруппировки.Структура);
			МодельТекста
					.ГруппировкаЗавершить()	
				.Добавить(".ГруппировкаЗавершить()")
			;
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

Процедура ВывестиВыбранныеПоля(МодельТекста, Элементы)
	Для Каждого ВыбранноеПоле Из Элементы Цикл
		Если ТипЗнч(ВыбранноеПоле) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			МодельТекста
				.Добавить(".ГруппаНачать(""{1}"")", ВыбранноеПоле.Заголовок)
					.ГруппировкаНачать()
			;
			ВывестиВыбранныеПоля(МодельТекста, ВыбранноеПоле.Элементы);
			МодельТекста
					.ГруппировкаЗавершить()
				.Добавить(".ГруппаЗавершить()")
			;
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ВыбранноеПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			МодельТекста
				.Добавить(".Поле(""*"")")
			;
		Иначе
			МодельТекста
				.Добавить(".Поле(""{1}"")", ВыбранноеПоле.Поле)
			;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ВывестиУсловияОтбора(МодельТекста, Элементы)
	Для Каждого ЭлементОтбора Из Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ Тогда
				МодельТекста
					.Добавить(".ОтборГруппаИНачать()")
				;
			ИначеЕсли ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли Тогда
				МодельТекста
					.Добавить(".ОтборГруппаИЛИНачать()")
				;
			Иначе
				МодельТекста
					.Добавить(".ОтборГруппаНЕНачать()")
				;
			КонецЕсли;
			МодельТекста
					.ГруппировкаНачать()
			;
			ВывестиУсловияОтбора(МодельТекста, ЭлементОтбора.Элементы);
			МодельТекста
					.ГруппировкаЗавершить()
				.Добавить(".ОтборГруппаЗавершить()")
			;
			Продолжить;
		КонецЕсли;
		Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			МодельТекста
				.Добавить(".Отбор(""{1}"", ""{2}"")", ЭлементОтбора.ЛевоеЗначение, ЭлементОтбора.ПравоеЗначение)
			;
		Иначе
			МодельТекста
				.Добавить(".Отбор(""{1}"", ""{2}"", ВидСравненияКомпоновкиДанных.{3})", ЭлементОтбора.ЛевоеЗначение, ЭлементОтбора.ПравоеЗначение, ОбщийКлиентСервер.CamelCase(ЭлементОтбора.ВидСравнения))
			;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
